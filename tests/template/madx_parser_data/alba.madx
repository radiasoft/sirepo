beam, SEQUENCE=Machine ,PARTICLE=electron, energy=3, RADIATE=True;
option, RBARC=FALSE;
!call, file= "ALBA-25.4th.madx";
TITLE "ALBA-24                          ";
!
! DATE AND TIME:    01/06/05  10.44.18
!
! FILE:             ALBA-25.mad

BK := -0.565618468391;
KD2 := -2.070584;
M1: MARKER;
M2: MARKER;
M3: MARKER;
!L_ID: DRIFT, L=3.985;
D11: DRIFT, L=0.17;
D12: DRIFT, L=0.15;
D13: DRIFT, L=0.15;
D14: DRIFT, L=0.375;
D15: DRIFT, L=0.26;
D21: DRIFT, L=0.26;
D22: DRIFT, L=0.15;
D23: DRIFT, L=0.375;
D24: DRIFT, L=0.15;
D25: DRIFT, L=0.165;
D26: DRIFT, L=0.47;
D28: DRIFT, L=0.26;
D31: DRIFT, L=0.26;
D32: DRIFT, L=0.37;
D33: DRIFT, L=0.175;
D34: DRIFT, L=0.15;
M_ID: DRIFT, L=2.096767;
D41: DRIFT, L=0.26;
D42: DRIFT, L=0.54;
D43: DRIFT, L=0.165;
S_ID: DRIFT, L=1.3;
QD1: QUADRUPOLE, L=0.23, K1=-1.773388770616;
QD2: QUADRUPOLE, L=0.29, K1=-1.877886175469;
QD3: QUADRUPOLE, L=0.29, K1=-1.849889294593;
QF1: QUADRUPOLE, L=0.29, K1=1.511188633956;
QF2: QUADRUPOLE, L=0.29, K1=1.901566889852;
QF3: QUADRUPOLE, L=0.29, K1=1.549700273264;
QF4: QUADRUPOLE, L=0.23, K1=1.492441234173;
QF5: QUADRUPOLE, L=0.31, K1=1.794343401905;
QF6: QUADRUPOLE, L=0.53, K1=2.078491948661;
QF7: QUADRUPOLE, L=0.53, K1=2.028150099179;
QF8: QUADRUPOLE, L=0.31, K1=2.008579189637;
B0: RBEND, L=1.383684, ANGLE=0.19634960897, K1=BK;
B1: RBEND, L=1.383684, ANGLE=0.19634960897, K1=BK;

SF1 : SEXTUPOLE, L=0.15, K2= 2.25116/0.15*2;
SD1 : SEXTUPOLE, L=0.15, K2=-3.79202/0.15*2;
SD2 : SEXTUPOLE, L=0.15, K2=-4.12417/0.15*2;
SF2 : SEXTUPOLE, L=0.22, K2= 5.9091/0.22*2;
SD3 : SEXTUPOLE, L=0.15, K2=-4.40757/0.15*2;
SD4 : SEXTUPOLE, L=0.22, K2=-6.20552/0.22*2;
SF3 : SEXTUPOLE, L=0.22, K2= 6.03784/0.22*2;
SD5 : SEXTUPOLE, L=0.15, K2=-4.4666/0.15*2;
SF4 : SEXTUPOLE, L=0.15, K2= 3.05743/0.15*2;


!RFC: RFCAVITY, L=0.0, VOLT=3.6, LAG=0.5, FREQ=1.998614156819E3, HARMON=448;
!RFC: MARKER;
! Put the RF cavity
RFC: RFCAVITY, L=0.5, VOLT=3.6, LAG=0.480,HARMON=448;
L_IDRF : DRIFT, L=3.985-0.5;
L_ID   : LINE =(RFC, L_IDRF);

B1in:  SBEND, L=B1->L/28, ANGLE=B1->ANGLE/28, K1=B1->K1, E1=B1->ANGLE/2, E2=0.0;
B1i:  SBEND, L=B1->L/28, ANGLE=B1->ANGLE/28, K1=B1->K1, E1=0.0, E2=0.0;
B1out: SBEND, L=B1->L/28, ANGLE=B1->ANGLE/28, K1=B1->K1, E1=0.0            , E2=B1->ANGLE/2;
BE: LINE=(B1in,B1i,B1i,B1i,B1i,B1i,B1i,B1i,&
          B1i, B1i,B1i,B1i,B1i,B1i,B1i,B1i,&
          B1i,B1i,B1i,B1i,B1i,B1i,B1i,B1i, &
          B1i,B1i,B1i,B1out);

!BE: LINE=(B0);
!B1i:  SBEND, L=B1->L/28, ANGLE=B1->ANGLE/28, K1=B1->K1, E1=0.0, E2=0.0;
!B1out: SBEND, L=B1->L/28, ANGLE=B1->ANGLE/28, K1=B1->K1, E1=0.0            , E2=B1->ANGLE/2;
!B1in:  SBEND, L=B1->L/28, ANGLE=B1->ANGLE/28, K1=B1->K1, E1=B1->ANGLE/2, E2=0.0;
!BE: LINE=(B1in,B1i,B1i,B1i,B1i,B1i,B1i,B1i,&
!          B1i, B1i,B1i,B1i,B1i,B1i,B1i,B1i,&
!          B1i,B1i,B1i,B1i,B1i,B1i,B1i,B1i, &
!          B1i,B1i,B1i,B1out);
BLOCK1: LINE=(L_ID,QD1,D11,QF1,D12,SF1,D13,QF2,D14,SD1,D15);
BLOCK2: LINE=(D21,SD2,D22,QF3,D23,QF4,D24,SF2,D25,QF5,D26,SD3,D28);
BLOCK31: LINE=(D31,SD4,D32,QF6,D33,SF3,D34,QD2,M_ID);
BLOCK32: LINE=(M_ID,QD3,D34,SF3,D33,QF7,D32,SD4,D31);
BLOCK4: LINE=(D41,SD5,D42,QF8,D43,SF4,S_ID);
CELDA: LINE=(BLOCK1,BE,BLOCK2,BE,BLOCK31,M1,BLOCK32,BE,BLOCK4,M2,-BLOCK4,BE,-BLOCK32,M3,BLOCK32,BE,BLOCK4,-BLOCK4,BE,-BLOCK32,-BLOCK31,BE,-BLOCK2,BE,-BLOCK1);
MACHINE: LINE=(CELDA);




!use, period=Machine;
!twiss;
!EMIT,DELTAP=0.0 ;

!The result of H emittance computed this way is: 0.11212603E-02 [pi micro m]
!But it is wrong due to the fact that the variations of the twiss functions inside
!the bendings  are not taken into account. To solve this the best thing to do
!is to slice the sbends in thick slices as follows:



use, period=Machine;
twiss;
EMIT,DELTAP=0.0 ;
!
!Now the emittance is correct: 0.44845315E-02
!
stop;
